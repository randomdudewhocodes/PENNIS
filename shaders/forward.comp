#version 450

layout(local_size_x = 256) in;

layout(set = 0, binding = 0) readonly  buffer Weights { float W[]; };
layout(set = 0, binding = 1) readonly  buffer Biases  { float B[]; };
layout(set = 0, binding = 2) readonly  buffer Inputs  { float X[]; };
layout(set = 0, binding = 3) writeonly buffer PreActs { float Z[]; };
layout(set = 0, binding = 4) writeonly buffer Acts    { float A[]; };

layout(push_constant) uniform PushConstants {
    uint inSize;
    uint outSize;
    uint actType;
} push;

float activate(float x)
{
    switch (push.actType)
    {
        case 1: return max(x, 0);
        case 2: return 1 / (1 + exp(-x));
        case 3: return tanh(x);
        default: return x;
    }
}

void main()
{
    uint id = gl_GlobalInvocationID.x;
    if(id >= push.outSize) return;

    float z = 0;
    for (uint i = 0; i < push.inSize; i++)
        z += X[i] * W[id * push.inSize + i];
    z += B[id];

    Z[id] = z;
    A[id] = activate(z);
}